import ILED.dzn;
import Timer.dzn;
import ISiren.dzn;
import IPwdManager.dzn;

interface IController {
  in void passwordEnterred(String pwd);
  in void sensorTriggered();

  behaviour {
    on passwordEnterred: {}
    on sensorTriggered: {}
  }
}

component Controller {
  provides IController iController;
  requires ITimer iTimer;
  requires ISiren iSiren;
  requires ILED iLed;
  requires IPwdManager iPwdManager;

  behaviour {
    enum State { Unarmed, Armed, Alarming };
    State state = State.Unarmed;

    [state.Unarmed] {
      on iController.passwordEnterred(pwd): {
      	bool valid = iPwdManager.verifyPassword(pwd);
      	if (valid){
      		state = State.Armed;
        	iLed.setYellow();
      	}
      }
      on iController.sensorTriggered(): {}
      on iTimer.timeout(): illegal;
    }
    [state.Armed] {
      on iController.passwordEnterred(pwd): {
      	bool valid = iPwdManager.verifyPassword(pwd);
      	if (valid) {
     		state = State.Unarmed;
        	iLed.setGreen();     		
      	}
      }
      on iController.sensorTriggered(): {
      	state = State.Alarming;
      	iTimer.start($30000$);
      	iLed.setRed();
      }
      on iTimer.timeout(): illegal;
    }
    [state.Alarming] {
      on iController.passwordEnterred(pwd): {
      	bool valid = iPwdManager.verifyPassword(pwd);
      	if (valid) {
	        state = State.Armed;
	        iLed.setYellow();
	        iSiren.turnOff();
	        iTimer.cancel();
      	}
      }
      on iController.sensorTriggered(): {}
      on iTimer.timeout(): {
      	iSiren.turnOn();
      }
    }
  }
}
